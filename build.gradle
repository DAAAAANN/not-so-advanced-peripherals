plugins {
	id 'fabric-loom' version '1.2-SNAPSHOT'
	id 'maven-publish'
	id "org.jetbrains.kotlin.jvm" version "1.8.21"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {

	// Add repositories to retrieve artifacts from in here.
	mavenLocal()
	mavenCentral()

	// Required for CC:R
	maven { url "https://jitpack.io" }

	// Required for Cobalt
	maven { url "https://squiddev.cc/maven" }

	// Required for Modmenu
	maven { url "https://maven.terraformersmc.com/releases/" }

	// Required fpr REI
	maven { url "https://maven.shedaniel.me/" }

	// Required for TechReborn and TechReborn Energy
	maven { url "https://maven.fabricmc.net/"}
	maven { url "https://maven.fabricmc.net/teamreborn/energy/"}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}+${project.minecraft_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

	// Depend upon CC Restitched
	modImplementation "com.github.cc-tweaked:cc-restitched:${project.ccr_version}"
	// Below disabled as it causes a crash on load. (Not sure why.)
	//modApi "com.github.cc-tweaked:cc-restitched:${project.ccr_version}"

	// Depend upon REI
	modImplementation "me.shedaniel:RoughlyEnoughItems-api:${project.rei_version}"

	// Depend upon Tech Reborn and Tech Reborn Energy API (not what they recommend but works)
	modImplementation "TechReborn:TechReborn-1.19:${project.tr_version}"
	modImplementation "teamreborn:energy:${project.tr_energy_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 17
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}